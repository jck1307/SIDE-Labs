-- @atlcompiler atl2006
module MindMap; -- Module Template
create OUT : MindMap from IN : RWM;

helper context String def : normalize() : String =
	self.regexReplaceAll('<([a-zA-Z0-9]*)>(.*)</\\1>','$2').regexReplaceAll('<([a-zA-Z0-9]*)>','')
	    .regexReplaceAll('&lt;([a-zA-Z0-9]*)>(.*)&lt;/\\1>','$2').regexReplaceAll('&lt;([a-zA-Z0-9]*)>','')
		.regexReplaceAll('\\\\(.)','$1').regexReplaceAll('&','&amp;').regexReplaceAll('"','&quot;');

rule definition {
  from s: RWM!RequirementsDefinition
  to t : MindMap!Maps (
	  maps <- RWM!Agent->allInstances()
  	)
}

rule agent {
	from s : RWM!Agent
	to t : MindMap!"Map" (
	  node <- t_node
  	),
	t_node : MindMap!Node (
	  text <- s.name+' - Objectifs',
	  sub <- s.isResponsible->collect(goal | thisModule.createNodeFromGoal(goal))
	)
}

rule createNodeFromGoal(s : RWM!Goal) {
	to t : MindMap!Node (
		text <- s.name,
		sub <- Sequence{description}->union(s.subgoals->collect(goal | thisModule.createNodeFromGoal(goal)))
	),
	description : MindMap!Node (
		text <- 
			if (s.documentation <> OclUndefined) then
				s.documentation.normalize()
			else
				''
			endif,
		presentation <- font_italic
	),
	font_italic : MindMap!NodePresentation (
		bold <- false,
		italic <- true,
		size <- 10,
		name <- 'SansSerif'
	)
	do {
		t;
	}
}