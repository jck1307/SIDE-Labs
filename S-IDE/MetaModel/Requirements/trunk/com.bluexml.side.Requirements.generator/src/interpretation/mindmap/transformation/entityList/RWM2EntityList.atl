-- @atlcompiler atl2006
-- List of agent
module MindMap; -- Module Template
create OUT : MindMap from IN : RWM;

helper context String def : normalize() : String =
	self.regexReplaceAll('<([a-zA-Z0-9]*)>(.*)</\\1>','$2').regexReplaceAll('<([a-zA-Z0-9]*)>','')
	    .regexReplaceAll('&lt;([a-zA-Z0-9]*)>(.*)&lt;/\\1>','$2').regexReplaceAll('&lt;([a-zA-Z0-9]*)>','')
		.regexReplaceAll('\\\\(.)','$1').regexReplaceAll('&','&amp;').regexReplaceAll('"','&quot;');

rule definition {
  from s: RWM!RequirementsDefinition
  to t : MindMap!"Map" (
	  node <- t_node
  	),
	t_node : MindMap!Node (
		text <- 'Structure d\'information',
		sub <- RWM!Entity->allInstances()
	)
}

rule entity {
	from s : RWM!Entity
	to t : MindMap!Node (
		text <- s.name,
		sub <- s.attributes,
		arrowlink <- RWM!RelationShip->allInstances()->select(r | r.source = s),
		id <- s.__xmiID__
	)
}

rule entityWithDocumentation extends entity {
	from s : RWM!Entity (s.documentation <> OclUndefined)
	to t : MindMap!Node (
		sub <- Sequence{description}->union(s.attributes)
	),
	description : MindMap!Node (
		text <- s.documentation.normalize(),
		presentation <- font_italic
	),
	font_italic : MindMap!NodePresentation (
		bold <- false,
		italic <- true,
		size <- 10,
		name <- 'SansSerif'
	)
}

rule attribute {
	from s : RWM!Attribute
	to t : MindMap!Node (
		text <- s.name
	)
}

rule attributeWithDocumentation extends attribute {
	from s : RWM!Attribute (s.documentation <> OclUndefined)
	to t : MindMap!Node (
		sub <- Sequence{description}
	),
	description : MindMap!Node (
		text <- s.documentation.normalize(),
		presentation <- font_italic
	),
	font_italic : MindMap!NodePresentation (
		bold <- false,
		italic <- true,
		size <- 10,
		name <- 'SansSerif'
	)    
}

rule relationship {
	from s : RWM!RelationShip
	to t : MindMap!ArrowLink (
		destination <- s.target,
		startarrow <- 'default',
		endarrow <- 'default'
	)
}