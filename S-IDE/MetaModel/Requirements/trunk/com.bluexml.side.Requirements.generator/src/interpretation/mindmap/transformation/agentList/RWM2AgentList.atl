-- @atlcompiler atl2006
-- List of agent
module AgentList_MindMap; -- Module Template
create OUT : MindMap from IN : RWM;

helper context String def : normalize() : String =
	self.regexReplaceAll('<([a-zA-Z0-9]*)>(.*)</\\1>','$2').regexReplaceAll('<([a-zA-Z0-9]*)>','')
	    .regexReplaceAll('&lt;([a-zA-Z0-9]*)>(.*)&lt;/\\1>','$2').regexReplaceAll('&lt;([a-zA-Z0-9]*)>','')
		.regexReplaceAll('\\\\(.)','$1').regexReplaceAll('&','&amp;').regexReplaceAll('"','&quot;');

rule definition {
  from s: RWM!RequirementsDefinition
  to t : MindMap!"Map" (
	  node <- t_node
  	),
	t_node : MindMap!Node (
		text <- 'Liste des agents',
		sub <- RWM!Agent->allInstances()
	)
}

rule agent {
	from s : RWM!Agent
	to t : MindMap!Node (
		text <- s.name
	)
}

rule agentWithDescription extends agent {
	from s : RWM!Agent(s.documentation <> OclUndefined)
	to t : MindMap!Node (
		sub <- Sequence{description}
	),
	description : MindMap!Node (
		text <- 
			if s.documentation <> OclUndefined then
				s.documentation.normalize()
			else
				''
			endif,
		presentation <- font_italic
	),
	font_italic : MindMap!NodePresentation (
		bold <- false,
		italic <- true,
		size <- 10,
		name <- 'SansSerif'
	)
}